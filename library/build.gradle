apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    buildToolsVersion "25.0.0"
    compileSdkVersion 25

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25

        versionCode 1
        versionName "1.0"

        consumerProguardFiles 'proguard-rules.txt'


        externalNativeBuild {
            cmake {
                cppFlags "-DANDROID_NDK -D_DEBUG"
                arguments "-DANDROID_STL=gnustl_shared"
            }
        }
        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jni.srcDirs = ['jni']
            jniLibs.srcDirs = ['libs']
        }

        instrumentTest.setRoot('tests')
    }

    lintOptions {
        abortOnError false
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

publish {
    userOrg = POM_DEVELOPER_ID
    groupId = GROUP
    artifactId = ARTIFACT_ID
    publishVersion = VERSION_NAME
    desc = POM_DESCRIPTION
    website = POM_URL
}
